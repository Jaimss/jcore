import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.5.10"
    id "org.cadixdev.licenser" version "0.6.0"
    id "com.github.johnrengelman.shadow" version "7.0.0"
    // id "me.bristermitten.pdm" version "0.0.33"
}

// delete build in the root directory too keep it clean
gradle.buildFinished {
    project.buildDir.deleteDir()
}

// subprojects setup
allprojects {
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "org.cadixdev.licenser"
    apply plugin: "com.github.johnrengelman.shadow"
    // apply plugin: "me.bristermitten.pdm"

    tasks.build.dependsOn tasks.updateLicenses

    // all subprojects have the same group/version as the main project
    group "dev.jaims.moducore"

    // version
    var ver = "0.6.6"
    var buildNumber = System.getenv().get("BUILD_NUMBER")
    if (buildNumber != null) version "$ver-b$buildNumber"
    version ver

    // shadow & set archive base name so only 1 jar generates
    jar {
        baseName "moducore-${projectDir.name}"
    }
    shadowJar {
        archiveClassifier.set("")
        baseName "moducore-${projectDir.name}"
        // relocate "me.bristermitten.pdm", "dev.jaims.moducore.libs.pdm"
    }

    // all projects have some shared dependencies & repositories
    repositories {
        mavenCentral()
        maven { url "https://repo.jaims.dev/repository/all/" } // all repositories
    }

    // dependencies required for all projects
    dependencies {
        compileOnly "org.jetbrains.kotlin:kotlin-stdlib"

        testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
    }

    // process resources to show the correct version
    processResources {
        with copySpec {
            from "src/main/resources"
            filter(ReplaceTokens, tokens: [
                    version: project.version
            ])
        }
    }

    // add license
    license {
        header = rootProject.file("HEADER.txt")
        include "**/*.kt", "**/*.java"
        newLine = true
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    // kotlin options
    compileKotlin {
        kotlinOptions.jvmTarget = "16"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "16"
    }
}
